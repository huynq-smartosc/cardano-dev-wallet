NPM_PACKAGE_VERSION := `jq '.version' package.json`

ARTEFACTS_DIR := "artefacts"
BUILD_DIR	:= "build"
SRC_DIR := "src"
CHROME_KEY := "dev-keys/chrome-dev-key.pem"
SUPPORTED_BROWSERS := "firefox chrome"

# List the available commands
@_list:
	just -ul

# Install the dependencies
setup:
	npm i

# Clean build artefacts
clean:
	rm -rf {{BUILD_DIR}}
	rm -rf {{ARTEFACTS_DIR}}


# Clean build artefacts and node_modules
clean-all:
	just clean

	rm -rf node_modules

# Check if the provided browser is supported by us
_check_browser browser:
	#!/usr/bin/env bash
	if echo {{SUPPORTED_BROWSERS}} | grep -qv {{browser}}; then
		echo "Unsupported browser: {{browser}}"
		echo "Supported browsers: {{SUPPORTED_BROWSERS}}"
		exit -1
	fi

# Build the project
build browser="firefox":
	#!/usr/bin/env bash
	set -euo pipefail
	just -q _check_browser {{browser}}

	mkdir -p {{BUILD_DIR}}

	echo ">> Copying artefacts .."
	cp -r {{SRC_DIR}}/* {{BUILD_DIR}}
	echo "Done."

	echo ">> Building WebExt JS files .."
	node esbuild.js
	echo "Done."

	echo ">> Copying manifest .."
	if [ {{browser}} = "firefox" ]; then
		mv {{BUILD_DIR}}/manifest.firefox.json {{BUILD_DIR}}/manifest.json
	elif [ {{browser}} = "chrome" ]; then
		mv {{BUILD_DIR}}/manifest.chrome.json {{BUILD_DIR}}/manifest.json
	fi

	rm {{BUILD_DIR}}/manifest.*.json
	echo "Done."


# Run the project
run browser="firefox":
	#!/usr/bin/env bash
	set -euo pipefail
	just -q _check_browser {{browser}}

	just build {{browser}}

	if [ {{browser}} = "firefox" ]; then
		npx web-ext run -s {{BUILD_DIR}} -t firefox-desktop --devtools
	elif [ {{browser}} = "chrome" ]; then
		npx web-ext run -s {{BUILD_DIR}} -t chromium --devtools
	fi

# Watch the source for changes and rebuild
dev browser="firefox":
	find {{SRC_DIR}} | entr just build {{browser}}

# Bundle the extension
bundle browser="firefox":
	#!/usr/bin/env bash
	set -euo pipefail
	just -q _check_browser {{browser}}

	just clean
	just build {{browser}}

	mkdir -p {{ARTEFACTS_DIR}}

	if [ {{browser}} = "firefox" ]; then
		npx web-ext build -s {{BUILD_DIR}} -a {{ARTEFACTS_DIR}}
	elif [ {{browser}} = "chrome" ]; then
		npx crx pack {{BUILD_DIR}} -o {{ARTEFACTS_DIR}}/web-extension-chrome-v{{NPM_PACKAGE_VERSION}}.crx --private-key {{CHROME_KEY}}
	fi

# Test the extension
test browser="firefox":
	#!/usr/bin/env bash
	set -euo pipefail
	just -q _check_browser {{browser}}

	just build {{browser}}


	if [ {{browser}} = "firefox" ]; then
		npx playwright test --browser firefox
	elif [ {{browser}} = "chrome" ]; then
		npx playwright test --browser chromium
	fi
